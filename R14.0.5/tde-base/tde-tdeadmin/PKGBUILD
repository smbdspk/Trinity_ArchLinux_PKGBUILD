# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tdeadmin
pkgver=14.0.5
pkgrel=1
pkgdesc="The TDE system administration utilities"
arch=('i686' 'x86_64')
url="http://scm.trinitydesktop.org/scm/git/${pkgname#*-}"
license=('GPL')
groups=('tde-base')
depends=('tde-tdebase' 'tde-libkexiv2' 'tde-libkdcraw')
makedepends=('pkgconfig' 'autoconf' 'imake')
optdepends=()
provides=("${pkgname#*-}")
conflicts=("trinity-${pkgname#*-}")
replaces=("${pkgname#*-}")
options=('staticlibs' 'libtool' '!emptydirs')
# install='pkgname.install'
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/tdeadmin-R${pkgver}.tar.bz2")
md5sums=('SKIP')


build() {

  cd ${srcdir}

  ## Environment config
  msg "Setting PATH, Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh       # or tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh   # or tde.sh
  export CFLAGS="${CFLAGS} -fpermissive"
  export CXXFLAGS="${CXXFLAGS} -fpermissive"
  export DO_NOT_COMPILE="knetworkconf"

  ## Generate config files and update with autoreconf
  cd ${srcdir}/${pkgname#*-}

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  ## configure
  msg "Configuring - ${pkgname}..."
  CFLAGS="${CFLAGS} -fpermissive" \
  CXXFLAGS="${CXXFLAGS} -fpermissive" \
  ./configure \
    --prefix=${TDEDIR} \
    --with-qt-dir=${QTDIR} \
    --with-qt-includes=${QTDIR}/include \
    --with-qt-libraries=${QTDIR}/lib \
    --with-extra-includes=${TDEDIR}/include/tde \
    --with-extra-libs=${TDEDIR}/lib/trinity \
    --localstatedir=/var \
    --enable-debug=full \
    --enable-closure

  ## make $NUMJOBS
  msg "Building - ${pkgname}..."
  make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"

  cd ${srcdir}/${pkgname#*-}            # use for non-out-of-source

  make -j1 DESTDIR="$pkgdir" install
}
