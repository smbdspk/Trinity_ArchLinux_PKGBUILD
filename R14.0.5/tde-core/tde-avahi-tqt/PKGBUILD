# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-avahi-tqt
pkgver=14.0.5
pkgrel=1
pkgdesc="Trinity Avahi-TQt"
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/avahi-tqt'
license=('GPL')
groups=('tde-core')
depends=('tde-tqtinterface' 'avahi' 'nss-mdns')
# nss-mdns should probably move to tdelibs depends
makedepends=('pkgconfig')
provides=('avahi-tqt')
replaces=('trinity-avahi-tqt')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/dependencies/avahi-tqt-R${pkgver}.tar.bz2")
md5sums=('SKIP')

build() {
  msg "Setting PATH and Trinity Environment variables"
  [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
  [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

  cd ${srcdir}

  ## adjust pkgconfig path
  export PKG_CONFIG_PATH="${PKG_CONFIG_PATH}:/opt/trinity/tqt3/lib/pkgconfig"

  cd ${srcdir}/dependencies/${pkgname#*-}

  msg "Starting configure..."
  ./autogen.sh --prefix=${TDEDIR} \
    --sysconfdir=${TDEDIR}/etc \
    --localstatedir=/var

  msg "Building - $pkgname..."
  make $NUMJOBS

}

package() {
  msg "Packaging - $pkgname"

  cd ${srcdir}/dependencies/${pkgname#*-}
  make -j1 DESTDIR="${pkgdir}" install
}
