# Contributor: e1z0 <justinas at eofnet dot lt>

pkgname=tde-tdeaddons
pkgver=14.0.5
pkgrel=1
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/tdeaddons'
license=('GPL')
groups=('tde-extra')
pkgdesc="Trinity Desktop Addons"
depends=('tde-tdebase')
makedepends=('pkgconfig' 'cmake' 'tde-tdegames' 'tde-tdepim' 'tde-tdemultimedia')
provides=('tdeaddons')
conflicts=('trinity-tdeaddons' 'trinity-kdeaddons')
replaces=('trinity-tdeaddons')
options=('staticlibs' 'libtool' '!strip')
source=("http://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/tdeaddons-R${pkgver}.tar.bz2")
md5sums=('b730d4d465d867755a19f77e6efc9e88')
# install=''

build() {
   msg "Setting PATH, CMAKE and Trinity Environment variables"
   # Source the QT and TDE profile
   [ "$QTDIR" = "" ] && . /usr/share/trinity/srcldr/tqt3.sh
   [ "$TDEDIR" = "" ] && . /usr/share/trinity/srcldr/trinity.sh

   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p build
   cd build

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-} \
      -DCMAKE_VERBOSE_MAKEFILE=ON \
      -DCMAKE_INSTALL_PREFIX=$TDEDIR \
      -DWITH_SPEEX=ON \
      -DWITH_ARTS=ON \
      -DBUILD_ALL=ON

   msg "Building - ${pkgname#*-}..."
   make $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   make -j1 DESTDIR="${pkgdir}" install
}
